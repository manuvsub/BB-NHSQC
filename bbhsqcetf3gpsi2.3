;hsqcetf3gpsi2
;avance-version (12/01/11)
;HSQC
;2D H-1/X correlation via double inept transfer
;   using sensitivity improvement
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;using f3 - channel
;with gradients in back-inept
;N15 180 pulses are replaced with broadband AI generated shaped pulses
;
;A.G. Palmer III, J. Cavanagh, P.E. Wright & M. Rance, J. Magn.
;   Reson. 93, 151-170 (1991)
;L.E. Kay, P. Keifer & T. Saarinen, J. Am. Chem. Soc. 114,
;   10663-5 (1992)
;J. Schleucher, M. Schwendinger, M. Sattler, P. Schmidt, O. Schedletzky,
;   S.J. Glaser, O.W. Sorensen & C. Griesinger, J. Biomol. NMR 4,
;   301-306 (1994)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple>

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"

/*******************************************************/
/**** parameters for N15 inversion/refocusing pulse ****/
/*******************************************************/
"p57 =p21*24.20"   
"spoff57=0.0"      
"spw57=plw3"       
"spoal57=0.5"       
;spnam57: pix_rf0.00_24.200p1_bw1.40p1.mrf
/*******************************************************/

/********************************************/
/**** C13 inversion pulse for decoupling ****/
/********************************************/
"p56=p3*10.021"   
"spw56=plw2"       
"spoff56=0.0"     
;spnam56: Set to z2iz_rf0.00_10.021p1_bw2.40B1.mrf;
/********************************************/


"d11=30m"
"d12=20u"
"d13=4u"
"d26=1s/(93.5*4)"
"d24=1s/(93.5*4)"	;1/(4J)YH for YH

"d0=inf1/4-2u" 	;start N15 dimension at 1/2 dwell time (90,-180 phase correction)
"in0=inf1/2"

"DELTA1=d13+p16+d16-p1*PI/3.14159+de+4u"
"DELTA2=d24-d13-p19-d16-p57/2"
"DELTA3=d26-d13-p16-d16-p57/2"

#   ifdef LABEL_CN
"DELTA=d13+p16+d16+larger(p2,p56)"
#   else
"DELTA=d13+p16+d16+p2"
#   endif /*LABEL_CN*/


"acqt0=0"
baseopt_echo


1 ze
  d11 pl16:f3
2 d1 do:f3
3 50u UNBLKGRAD
  d12 pl1:f1
#   ifdef LABEL_CN
  d12 pl2:f2
#   endif /*LABEL_CN*/

;;;
  (p1 ph20):f1
;;;
  d13
  p16:gp6
  d16
  DELTA3 pl0:f3
  (center (p2 ph20):f1 (p57:sp57 ph6):f3)
  DELTA3 
  d13
  p16:gp6
  d16 pl3:f3
;;;
  (p1 ph21):f1
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#   ifndef noFLIP
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; WATER FLIPBACK PULSE 
;; not required for samples in organic solvent
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  4u
  (p11:sp1 ph11:r):f1
#   ifdef FLIP1		/* flag for calibrating flipback pulse (spdb1/ph11) */
  4u pl16:f3
  goto 9
#   endif /*FLIP1*/
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#   endif /*noFLIP*/
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  d13
  p17:gp1
  d16 pl1:f1
;;;
  (p21 ph1):f3
;;; N15 EVOLUTION BEGINS
  d0 gron0
  2u groff
#   ifdef LABEL_CN
  (center (p2 ph20) (p56:sp56 ph20):f2 )
#   else
  (p2 ph20)
#   endif /*LABEL_CN*/
  d13
  p16:gp2
  d16

  d0 gron0*-1
  2u groff

  ;;;(p22 ph7):f3
  (p57:sp57 ph7):f3

  DELTA pl3:f3

;;; N15 EVOLUTION ENDS
  (center (p1 ph22) (p21 ph4):f3 )
;;;
  d13
  p19:gp4
  d16
  DELTA2
  ;;;(center (p2 ph20) (p22 ph20):f3 )
  (center (p2 ph20) (p57:sp57 ph20):f3 )
  DELTA2
  d13
  p19:gp4
  d16 pl3:f3
;;;
  (center (p1 ph21) (p21 ph5):f3 )
;;;
  d13
  p16:gp5
  d16
  DELTA3 pl0:f3
  ;;;(center (p2 ph20) (p22 ph20):f3 )
  (center (p2 ph20) (p57:sp57 ph20):f3 )
  DELTA3
  d13
  p16:gp5
  d16
;;;
  (p1 ph20)
;;;
  DELTA1
  (p2 ph20)
  d13
  p16:gp3*EA
  d16 pl16:f3

9 4u BLKGRAD

#   ifdef decN15		/* flag for turning on N15 decoupling during acqusition */
  go=2 ph31 cpd3:f3
#   else  /*decN15*/
  go=2 ph31 
#   endif /*decN15*/
  d1 do:f3 mc #0 to 2 
     F1EA(calgrad(EA) & calph(ph5, +180), caldel(d0, +in0) & calph(ph1, +180) & calph(ph6, +180) & calph(ph31, +180))
exit
   

ph1=0 2
ph4=0 0 2 2
ph5=1 1 3 3
ph6=0
ph7=0 0 0 0 1 1 1 1 2 2 2 2 3 3 3 3

/* water flipback phase */
ph11=0

ph31=0 2 2 0 2 0 0 2

ph20=0
ph21=1
ph22=2
ph23=3


; ~~ WaveMaker ~~~~~~~~~~~
#   ifdef decN15
;cpd3:wvm:garp16(120us)
#   endif /*decN15*/

  

;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8:  f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p28: f1 channel - trim pulse                          [1 msec]
;d0 : incremented delay (2D)                           [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d24: 1/(4J)YH for YH
;     1/(8J)YH for all multiplicities
;d26: 1/(4J(YH))
;cnst4: = J(YH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;ns: 1 * n
;ds: >= 16
;td1: number of experiments
;FnMODE: echo-antiecho
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 2 : gp 3 
;			  80 : 20.1    for C-13
;			  80 :  8.1    for N-15

;for z-only gradients:
;gpz0: -2%
;gpz1: -61%
;gpz2: 80%
;gpz3: 8.1% for N-15, 20.1% for C-13
;gpz4: 11%
;gpz5: 17%
;gpz6: 23%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)

;noFLIP : turn off water flipback pulse (e.g. if you sample is in organic solvent)

;FLIP1 : to calibrate power level (spdb1) and phase (phcor11) for water flipback pulse;
;	-turn experiment into 1D
;	-set ds 0 ; ns 1 ; rg 4
;	-turn off water filter during processing
;	-use popt to calibrate spdb1 (power level) and phcor11 (phase correction)
;	-remember to remove flag at the end

;decN15 : turn on N15 decoupling during acquisition
                                         ;preprocessor-flags-end



;$Id: hsqcetf3gpsi2,v 1.6 2012/01/31 17:49:26 ber Exp $
